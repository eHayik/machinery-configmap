buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'ru.vyarus.pom' version '2.1.0'
    id 'org.springframework.boot' version "${springBoot}"
    id 'io.spring.dependency-management' version "${dependencyManagement}"
    id 'com.github.sherter.google-java-format' version '0.9'
    id 'pl.allegro.tech.build.axion-release' version '1.13.2'
    id 'org.sonarqube' version '3.2.0'
    id 'jacoco'
}

project.version = scmVersion.version

jacoco {
    toolVersion = "0.8.2"
}

googleJavaFormat {
    options style: 'AOSP'
}

compileJava.dependsOn 'googleJavaFormat'

sonarqube {
    properties {
        property "sonar.projectKey", "eljaiek_machinery-configmap"
        property "sonar.organization", "eljaiek"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {
    repositories {
        mavenCentral()
    }

    bootJar {
        enabled = false
    }
}

subprojects {

    group = 'com.github.ehayik.toolbelt'
    version = rootProject.version

    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'ru.vyarus.pom'
    apply plugin: 'jacoco'
    apply from: "$rootDir/gradle/ext/publish-common.gradle"

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testImplementation(platform("org.junit:junit-bom:${junit}"))
    }

    test {
        useJUnitPlatform()
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }

    java {
        withSourcesJar()

        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }
    
    jacocoTestReport {
        reports {
            xml.required.set(true)
        }
    }

    test.finalizedBy jacocoTestReport

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom { x -> pom(x) }
            }
        }

        repositories {
            maven { x -> nexusRepository(x) }
        }
    }
}

file("toolbelt-configmap-sources").eachDir { x ->
    project(":${x.name}") {
        dependencies {
            implementation project(':toolbelt-configmap-core')
            implementation "org.eclipse.collections:eclipse-collections:${eclipseCollectionsApi}"
            implementation "org.eclipse.collections:eclipse-collections-api:${eclipseCollectionsApi}"

            testImplementation 'org.junit.jupiter:junit-jupiter'
            testImplementation "org.assertj:assertj-core:${assertjCore}"
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
        }
    }
}

file("toolbelt-configmap-starters").eachDir { x ->
    project(":${x.name}") {
        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter'
            api project(':toolbelt-configmap-core')
            api (project(':toolbelt-configmap-autoconfigure')) {
                exclude module: 'jackson-datatype-toolbelt-configmap'
            }
        }
    }
}

task publishAll(type: GradleBuild) {
    tasks = [ 'publish' ]
}

task publishAllToMavenLocal(type: GradleBuild) {
    tasks = [ 'publishToMavenLocal' ]
}